# TestingSetup.cmake
# This file handles creating symbolic link to model and image files for testing

# Include shared model configuration
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/Config.cmake)

# Derived paths
set(MODEL_LINK_PATH ${CMAKE_CURRENT_BINARY_DIR}/${MODEL_FILE})

# Create symbolic link to model file in build directory for testing
add_custom_command(
  OUTPUT ${MODEL_LINK_PATH}
  COMMAND ${CMAKE_COMMAND} -E create_symlink
          ${MODEL_PATH}
          ${MODEL_LINK_PATH}
  DEPENDS scnn_torch_backend  # Depend on the main library target so it won't build before the model is generated
  COMMENT "Creating symbolic link to model file for testing: ${MODEL_FILE}..."
)

# Custom target for the symbolic link
add_custom_target(test_model_link
  DEPENDS ${MODEL_LINK_PATH}
)

# Initialize an empty list to collect output files
set(TEST_IMAGE_OUTPUTS)

# Loop to create a symbolic link for each image file in build directory for testing
foreach(image_file IN LISTS TEST_IMAGE_FILES)
  set(src ${CMAKE_CURRENT_SOURCE_DIR}/test/${image_file})
  set(dst ${CMAKE_CURRENT_BINARY_DIR}/${image_file})

  add_custom_command(
    OUTPUT ${dst}
    COMMAND ${CMAKE_COMMAND} -E create_symlink ${src} ${dst}
    DEPENDS scnn_torch_backend
    COMMENT "Creating symbolic link to ${image_file} for testing..."
  )

  list(APPEND TEST_IMAGE_OUTPUTS ${dst})
endforeach()

# Custom target that depends on all symbolic links
add_custom_target(test_image_link
  DEPENDS ${TEST_IMAGE_OUTPUTS}
)

# Function to add testing dependency to a target
function(add_testing_dependency target_name)
  add_dependencies(${target_name} test_model_link test_image_link)
endfunction()
